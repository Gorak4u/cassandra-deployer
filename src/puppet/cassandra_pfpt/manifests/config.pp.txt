# @summary Manages Cassandra configuration files and OS tuning.
class cassandra_pfpt::config inherits cassandra_pfpt {
  file { [$data_dir, $commitlog_dir, $saved_caches_dir, $hints_directory, $cdc_raw_directory]:
    ensure  => 'directory',
    owner   => $user,
    group   => $group,
    mode    => '0700',
    require => Package['cassandra'],
  }
  file { '/root/.cassandra':
    ensure => 'directory',
    owner  => 'root',
    group  => 'root',
    mode   => '0700',
  }
  file { $jamm_target:
    ensure  => 'file',
    owner   => 'root',
    group   => 'root',
    mode    => '0644',
    source  => $jamm_source,
    require => Package['cassandra'],
  }
  file { '/etc/cassandra/conf/cassandra.yaml':
    ensure  => 'file',
    content => template('cassandra_pfpt/cassandra.yaml.erb'),
    owner   => $user,
    group   => $group,
    mode    => '0644',
    require => Package['cassandra'],
    notify  => Class['cassandra_pfpt::service'],
  }
  file { '/etc/cassandra/conf/cassandra-rackdc.properties':
    ensure  => 'file',
    content => template('cassandra_pfpt/cassandra-rackdc.properties.erb'),
    owner   => $user,
    group   => $group,
    mode    => '0644',
    require => Package['cassandra'],
    notify  => Class['cassandra_pfpt::service'],
  }
  file { '/etc/cassandra/conf/jvm-server.options':
    ensure  => 'file',
    content => template('cassandra_pfpt/jvm-server.options.erb'),
    owner   => $user,
    group   => $group,
    mode    => '0644',
    require => Package['cassandra'],
    notify  => Class['cassandra_pfpt::service'],
  }
  file { '/root/.cassandra/cqlshrc':
    ensure  => 'file',
    content => template('cassandra_pfpt/cqlshrc.erb'),
    owner   => 'root',
    group   => 'root',
    mode    => '0600',
    require => File['/root/.cassandra'],
  }
  file { $manage_bin_dir:
    ensure => 'directory',
    owner  => 'root',
    group  => 'root',
    mode   => '0755',
  }
  [ 'cluster-health.sh',
    'cleanup-node.sh', 'take-snapshot.sh', 'drain-node.sh', 'rebuild-node.sh',
    'garbage-collect.sh', 'assassinate-node.sh', 'upgrade-sstables.sh',
    'full-backup-to-s3.sh', 'incremental-backup-to-s3.sh', 'prepare-replacement.sh', 'version-check.sh', 'backup-status.sh',
    'cassandra_range_repair.py', 'range-repair.sh',
    'restore-from-s3.sh', 'node_health_check.sh', 'rolling_restart.sh',
    'disk-health-check.sh', 'decommission-node.sh', 'compaction-manager.sh',
    'cassandra-admin.sh', 'stress-test.sh', 'stop-node.sh', 'reboot-node.sh', 'cassandra-manual.sh', 'cassandra-upgrade-precheck.sh', 'cassandra_facts.sh' ].each |$script| {
    file { "${$manage_bin_dir}/${$script}":
      ensure  => 'file',
      source  => "puppet:///modules/cassandra_pfpt/${$script}",
      owner   => 'root',
      group   => 'root',
      mode    => '0755',
      require => File[$manage_bin_dir],
    }
  }
  if $disable_swap {
    exec { 'swapoff -a':
      command => '/sbin/swapoff -a',
      unless  => '/sbin/swapon -s | /bin/grep -qE "^/[^ ]+\s+partition\s+0\s*$"',
      path    => ['/usr/bin', '/usr/sbin', '/bin', '/sbin'],
    }
    augeas { 'fstab_no_swap':
      context => '/files/etc/fstab',
      changes => 'set */[spec="swap"]/#comment "swap"',
      onlyif  => 'get */[spec="swap"] != ""',
      require => Exec['swapoff -a'],
    }
    $merged_sysctl = $sysctl_settings + { 'vm.swappiness' => 0 }
  } else {
    $merged_sysctl = $sysctl_settings
  }
  if !empty($merged_sysctl) {
    file { '/etc/sysctl.d/99-cassandra.conf':
      ensure  => 'file',
      content => template('cassandra_pfpt/sysctl.conf.erb'),
      notify  => Exec['apply_sysctl_cassandra'],
    }
    exec { 'apply_sysctl_cassandra':
      command     => '/sbin/sysctl -p /etc/sysctl.d/99-cassandra.conf',
      path        => ['/usr/bin', '/usr/sbin', '/bin', '/sbin'],
      refreshonly => true,
    }
  }
  if !empty($limits_settings) {
    file { '/etc/security/limits.d/cassandra.conf':
      ensure  => 'file',
      content => template('cassandra_pfpt/cassandra_limits.conf.erb'),
    }
  }
  if $manage_jmx_security {
    file { $jmx_password_file_path:
      ensure  => 'file',
      content => $jmx_password_file_content,
      owner   => $user,
      group   => $group,
      mode    => '0400',
      require => Package['cassandra'],
      notify  => Class['cassandra_pfpt::service'],
    }
    file { $jmx_access_file_path:
      ensure  => 'file',
      content => $jmx_access_file_content,
      owner   => $user,
      group   => $group,
      mode    => '0400',
      require => Package['cassandra'],
      notify  => Class['cassandra_pfpt::service'],
    }
  }
  if $facts['service_provider'] == 'systemd' {
    file { '/etc/systemd/system/cassandra.service.d':
      ensure => 'directory',
      owner  => 'root',
      group  => 'root',
      mode   => '0755',
    }
    file { '/etc/systemd/system/cassandra.service.d/override.conf':
      ensure  => 'file',
      content => template('cassandra_pfpt/cassandra.service.d.erb'),
      owner   => 'root',
      group   => 'root',
      mode    => '0644',
      notify  => Exec['cassandra-systemd-reload'],
      require => File['/etc/systemd/system/cassandra.service.d'],
    }
    exec { 'cassandra-systemd-reload':
      command     => 'systemctl daemon-reload',
      path        => ['/bin', '/usr/bin'],
      refreshonly => true,
      before      => Class['cassandra_pfpt::service'],
    }
  }
}
