# @summary Manages the Cassandra service.
# This file is managed by Puppet.
class cassandra_pfpt::service inherits cassandra_pfpt {
  service { 'cassandra':
    ensure     => running,
    enable     => true,
    hasstatus  => true,
    hasrestart => true,
    require    => [
      Package['cassandra'],
      File['/etc/cassandra/conf/cassandra.yaml'],
      File['/etc/cassandra/conf/cassandra-rackdc.properties'],
      File['/etc/cassandra/conf/jvm-server.options'],
    ],
  }
  file { $change_password_cql:
    ensure  => 'file',
    content => "ALTER USER cassandra WITH PASSWORD '${$cassandra_password.unwrap}';",
    owner   => 'root',
    group   => 'root',
    mode    => '0600',
  }
  $cqlsh_ssl_opt = $ssl_enabled ? {
    true  => '--ssl',
    false => '',
  }
  # Change only if new password isn't already active
  exec { 'change_cassandra_password':
    command     => "cqlsh ${cqlsh_ssl_opt} -u cassandra -p cassandra -f ${change_password_cql}",
    path        => ['/bin', '/usr/bin', $cqlsh_path_env],
    timeout     => 60,
    tries       => 2,
    try_sleep   => 10,
    logoutput   => on_failure,
    # If we can connect with the *new* password, skip running the ALTER
    unless      => "cqlsh ${cqlsh_ssl_opt} -u cassandra -p '${cassandra_password.unwrap}' -e \"SELECT cluster_name FROM system.local;\" >/dev/null 2>&1",
    onlyif      => "${manage_bin_dir}/cluster-health.sh --silent",
    require     => [ Service['cassandra'], File[$change_password_cql] ],
  }
}
