# @summary Manages Java installation for Cassandra.
class cassandra_pfpt::java inherits cassandra_pfpt {
  if $java_package_name and $java_package_name != '' {
    $actual_java_package = $java_package_name
  } else {
    $actual_java_package = $facts['os']['family'] ? {
      'RedHat' => $java_version ? {
        '8'     => 'java-1.8.0-openjdk-headless',
        default => "java-${java_version}-openjdk-headless",
      },
      'Debian' => "openjdk-${java_version}-jre-headless",
      default  => fail("Unsupported OS family for automatic Java package selection: ${facts['os']['family']}"),
    }
  }

  package { $actual_java_package:
    ensure  => 'present',
  }
}
